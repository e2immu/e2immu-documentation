/*
 * e2immu: a static code analyser for effective and eventual immutability
 * Copyright 2020-2021, Bart Naudts, https://www.e2immu.org
 *
 * This program is free software: you can redistribute it and/or modify it under the
 * terms of the GNU Lesser General Public License as published by the Free Software
 * Foundation, either version 3 of the License, or (at your option) any later version.
 * This program is distributed in the hope that it will be useful, but WITHOUT ANY
 * WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
 * FOR A PARTICULAR PURPOSE.  See the GNU Lesser General Public License for
 * more details. You should have received a copy of the GNU Lesser General Public
 * License along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

plugins {
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
    id 'org.asciidoctor.jvm.pdf' version '3.3.2'
}

// "com.google.guava:guava:32.1.3-jre"

repositories {
    mavenCentral()
}

asciidoctor {
    sources {
        include 'manual.adoc'
        include 'road-to-immutability.adoc'
    }
    resources {
        from(sourceDir) {
            include 'images/*.png'
        }
    }
}

build.dependsOn ('asciidoctor', 'asciidoctorPdf')

def docs = '../e2immu-site/static/docs/'

tasks.register('copyManualToSite', Copy) {
    from 'build/docs/asciidoc/manual.html'
    into docs
    dependsOn(asciidoctor)
}

tasks.register('copyManualPDFToSite', Copy) {
    from file('build/docs/asciidocPdf/manual.pdf')
    into docs
    dependsOn(asciidoctorPdf)
}

tasks.register('copyRoadToSite', Copy) {
    from 'build/docs/asciidoc/road-to-immutability.html'
    into docs
    dependsOn(asciidoctor)
}

tasks.register('copyRoadPDFToSite', Copy) {
    from file('build/docs/asciidocPdf/road-to-immutability.pdf')
    into docs
    dependsOn(asciidoctorPdf)
}

tasks.register('copyToSite') {
    dependsOn(copyManualToSite, copyRoadToSite, copyManualPDFToSite, copyRoadPDFToSite)
}

